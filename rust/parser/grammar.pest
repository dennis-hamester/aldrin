WHITESPACE = _{ WHITE_SPACE }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
ws = _{ WHITESPACE | COMMENT }

lit_pos_nonzero_int = @{ ('1'..'9') ~ ('0'..'9')* }
lit_int = @{ "0" | ("-"? ~ lit_pos_nonzero_int) }

lit_string_char = @{ "\\\"" | (!("\"" | NEWLINE) ~ ANY) }
lit_string = @{ "\"" ~ lit_string_char* ~ "\"" }

lit_uuid = @{
    ASCII_HEX_DIGIT{8}
    ~ "-" ~ ASCII_HEX_DIGIT{4}
    ~ "-" ~ ASCII_HEX_DIGIT{4}
    ~ "-" ~ ASCII_HEX_DIGIT{4}
    ~ "-" ~ ASCII_HEX_DIGIT{12}
}

tok_term = @{ ";" }
tok_eq = @{ "=" }
tok_par_open = @{ "(" }
tok_par_close = @{ ")" }
tok_ang_open = @{ "<" }
tok_ang_close = @{ ">" }
tok_arrow = @{ "->" }
tok_scope = @{ "::" }
tok_hash = @{ "#" }
tok_squ_open = @{ "[" }
tok_squ_close = @{ "]" }
tok_comma = @{ "," }

kw_import = @{ "import" ~ ws }
kw_const = @{ "const" ~ ws }
kw_u8 = @{ "u8" }
kw_i8 = @{ "i8" }
kw_u16 = @{ "u16" }
kw_i16 = @{ "i16" }
kw_u32 = @{ "u32" }
kw_i32 = @{ "i32" }
kw_u64 = @{ "u64" }
kw_i64 = @{ "i64" }
kw_string = @{ "string" }
kw_uuid = @{ "uuid" }
kw_bool = @{ "bool" }
kw_f32 = @{ "f32" }
kw_f64 = @{ "f64" }
kw_value = @{ "value" }
kw_vec = @{ "vec" }
kw_bytes = @{ "bytes" }
kw_map = @{ "map" }
kw_set = @{ "set" }

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
schema_name = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" | "-")* }

vec_type = { kw_vec ~ tok_ang_open ~ type_name ~ tok_ang_close }
map_type = { kw_map ~ tok_ang_open ~ key_type_name ~ tok_arrow ~ type_name ~ tok_ang_close }
set_type = { kw_set ~ tok_ang_open ~ key_type_name ~ tok_ang_close }
external_type_name = { schema_name ~ tok_scope ~ ident }

key_type_name = {
    kw_u8
    | kw_i8
    | kw_u16
    | kw_i16
    | kw_u32
    | kw_i32
    | kw_u64
    | kw_i64
    | kw_string
    | kw_uuid
}
type_name = {
    kw_bool
    | kw_u8
    | kw_i8
    | kw_u16
    | kw_i16
    | kw_u32
    | kw_i32
    | kw_u64
    | kw_i64
    | kw_f32
    | kw_f64
    | kw_string
    | kw_uuid
    | kw_value
    | vec_type
    | kw_bytes
    | map_type
    | set_type
    | external_type_name
    | ident
}

file = _{
    SOI
    ~ import_stmt*
    ~ const_def*
    ~ EOI
}

attribute = {
    tok_hash ~ tok_squ_open ~ ident
    ~ (tok_par_open ~ ident ~ (tok_comma ~ ident)* ~ tok_comma? ~ tok_par_close)?
    ~ tok_squ_close
}

import_stmt = { kw_import ~ schema_name ~ tok_term }

const_def = { kw_const ~ ident ~ tok_eq ~ const_value ~ tok_term }
const_value = {
    const_u8
    | const_i8
    | const_u16
    | const_i16
    | const_u32
    | const_i32
    | const_u64
    | const_i64
    | const_string
    | const_uuid
}
const_u8 = { kw_u8 ~ tok_par_open ~ lit_int ~ tok_par_close }
const_i8 = { kw_i8 ~ tok_par_open ~ lit_int ~ tok_par_close }
const_u16 = { kw_u16 ~ tok_par_open ~ lit_int ~ tok_par_close }
const_i16 = { kw_i16 ~ tok_par_open ~ lit_int ~ tok_par_close }
const_u32 = { kw_u32 ~ tok_par_open ~ lit_int ~ tok_par_close }
const_i32 = { kw_i32 ~ tok_par_open ~ lit_int ~ tok_par_close }
const_u64 = { kw_u64 ~ tok_par_open ~ lit_int ~ tok_par_close }
const_i64 = { kw_i64 ~ tok_par_open ~ lit_int ~ tok_par_close }
const_string = { kw_string ~ tok_par_open ~ lit_string ~ tok_par_close }
const_uuid = { kw_uuid ~ tok_par_open ~ lit_uuid ~ tok_par_close }
